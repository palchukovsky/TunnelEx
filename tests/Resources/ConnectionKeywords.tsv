*Settings*
Library	OperatingSystem
Library	Collections	
Resource	CommonKeywords.tsv

*Variables*							
${CONNECTIONS TEST TYPE}	${EMPTY}						
${CONNECTIONS TEST FILTER}	${EMPTY}						
							
*Keywords*							
Start ${type} connections server							
	Set connections test type	${type}					
	Start project process						
	Start unit tests process	${CONNECTIONS TEST TYPE}-server	connections server				
							
Set connections test type							
	[Arguments]	${type}					
	${possible values}	Create Dictionary	TCP	tcp	UDP	udp	pipe
	...	pipe					
	Dictionary Should Contain Key	${possible values}	${type}	Unknown type of client or server.			
	${type}	Get From Dictionary	${possible values}	${type}			
	Set Test Variable	${CONNECTIONS TEST TYPE}	${type}				
							
Set TCP connections test filter							
	[Arguments]	${test}					
	${possible values}	Create Dictionary	active data exchange	DataExchangeActive	passive data exchange	DataExchangePassive	active one-way data exchange
	...	DataExchangeOneWayActive	passive one-way data exchange	DataOneWayExchangePassive	several connections	SeveralConnetions	
	Dictionary Should Contain Key	${possible values}	${test}	Unknown unit test.			
	${filter}	Get From Dictionary	${possible values}	${test}			
	Set Test Variable	${CONNECTIONS TEST FILTER}	${filter}				
							
Wait for connections test end and check result							
	Switch Process	connections client					
	${client output}	Read Process Output					
	Log	${client output}					
	Stop All Processes						
							
Should be set connections test type							
	${is set}	Convert To Boolean	${CONNECTIONS TEST TYPE}				
	Run Keyword Unless	${is set}	Fail	Connection test type should be set at each test setup.			
							
Test connections with ${test}							
	Should be set connections test type						
	Run Keyword	Set ${CONNECTIONS TEST TYPE} connections test filter	${test}				
	Run unit tests and check RC and output	${CONNECTIONS TEST TYPE}-client --gtest_filter=${CONNECTIONS TEST FILTER}					
							
Stop connections server							
	Should be set connections test type						
	Switch Process	connections server					
	${test server output}	Read Process Output					
	Should be unit test completed successfully	${test server output}					
							
